---

Created at: 2021-09-24
Last updated at: 2021-11-10
Source URL: https://stackoverflow.com/questions/29294045/how-to-open-two-instances-of-chrome-kiosk-mode-in-different-displays-windows
tags: 
  - shell
  - cmd
  - 浏览器
  - 全屏
  - vb

---

# 奇偶岸边


#### 浏览器启动参数

		演示模式 `--kiosk`


		全屏启动 `--start-fullscreen`
	
		最大化启动 `--start-maximized`
	
		指定位置启动 `--window-position=x,y`
	
		指定窗口大小启动 `--window-size=w,h`
	
		用户文件存储位置 `--user-data-dir=f:/monitor1`
	
		影子模式 `--incognito`


#### 一键脚本

注：一定要按顺序，不然不生效
用户文件存储位置配好才能显示两个屏，不然会出现在上一个屏上

* 生成脚本

    @echo off
    ::浏览器安装路径
    set tempTargetPath="C:\Program Files (x86)\Google\Chrome\Application\chrome.exe"
    ::需要修改的变量
    if "%~1"=="" (set windowPosition=0,0) else (set windowPosition=%~1)
    if "%~2"=="" (set portIndex=0) else (set portIndex=%~2)
    if "%~3"=="" (set configInfo="") else (set configInfo=%~3)
    ::不用修改的变量
    set tempHtmlUrl=%~dp0loading.html
    set tempHtmlUrl=%tempHtmlUrl:\=/%
    set tempUserDataDir=%~dp0monitor/%portIndex%
    set tempLink=monitor_%portIndex%.lnk
    ::本机IP和时间
    ::for /f "tokens=16" %%i in ('ipconfig ^|find /i "ipv4"') do set clientIP=%%i
    set tempArguments=" --app=%tempHtmlUrl%?p=%portIndex%&c=%configInfo:"=% --window-position=%windowPosition% --kiosk --user-data-dir=%tempUserDataDir% portIndex%portIndex%"
    ::删除 临时目录 下所有文件
    ::del /s /f /q "%~dp0monitor\%portIndex%\*.*"
    ::删除 临时目录 下所有文件夹
    ::rd /s /q "%~dp0monitor\%portIndex%"
    ::删除 临时快捷方式
    ::del %tempLink%
    ::场景临时目录
    md %~dp0monitor
    md %tempUserDataDir%
    ::创建生成脚本
    echo Set oWS=WScript.CreateObject("WScript.Shell") > CreateShortcut_%portIndex%.vbs
    echo Set oLink=oWS.CreateShortcut("%tempLink%") >> CreateShortcut_%portIndex%.vbs
    echo oLink.TargetPath=%tempTargetPath% >> CreateShortcut_%portIndex%.vbs
    echo oLink.Arguments=%tempArguments% >> CreateShortcut_%portIndex%.vbs
    echo oLink.Save >> CreateShortcut_%portIndex%.vbs
    echo oWS.Run "%tempLink%" >> CreateShortcut_%portIndex%.vbs
    ::执行生成脚本
    cscript CreateShortcut_%portIndex%.vbs
    ::删除生成脚本
    del CreateShortcut_%portIndex%.vbs
    
* 销毁单个脚本

    @echo off
    ::需要修改的变量
    if "%~1"=="" (set portIndex=0) else (set portIndex=%~1)
    wmic process where "name='chrome.exe' and commandline like '%%portIndex%portIndex%%%'" delete
    ::不用修改的变量
    set tempLink=monitor_*.lnk
    ::删除 临时目录 下所有文件
    del /s /f /q "%~dp0monitor\%portIndex%\*.*"
    ::删除 临时目录 下所有文件夹
    rd /s /q "%~dp0monitor\%portIndex%"
    ::删除 临时快捷方式
    del %tempLink%
    
* 销毁全部脚本

    @echo off
    ::需要修改的变量
    if "%~1"=="" (set tempExe=chrome.exe) else (set tempExe=%~1)
    taskkill /f /im %tempExe%
    ::不用修改的变量
    set tempLink=monitor_*.lnk
    ::删除 临时目录 下所有文件
    del /s /f /q "%~dp0monitor\*.*"
    ::删除 临时目录 下所有文件夹
    rd /s /q "%~dp0monitor"
    ::删除 临时快捷方式
    del %tempLink%
    exit
    

